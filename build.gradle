buildscript {
    ext.kotlin_version = '1.2.31'
    ext.spring_boot_version = '1.5.10.RELEASE'
    ext.fasterxml_version = '2.9.5'
    ext.spring_kafka_version = '2.1.4.RELEASE'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

plugins {
    id "jacoco"
}

group 'jalgoarena'
description = """JAlgoArena Queue Service"""

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/versioning.gradle"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.kafka:spring-kafka",
            "org.springframework.cloud:spring-cloud-starter-eureka",
            "com.fasterxml.jackson.core:jackson-databind:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-core:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-annotations:$fasterxml_version",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$fasterxml_version",
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",
            "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile 'junit:junit:4.12',
                "org.springframework.kafka:spring-kafka-test:$spring_kafka_version",
                "org.springframework.boot:spring-boot-starter-test",
                'com.nhaarman:mockito-kotlin-kt1.1:1.5.0',
                "org.assertj:assertj-core:3.9.1"
}

jar {
    baseName = 'jalgoarena-queue'
}

apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'clean', 'bootRepackage'

bootRun {
    systemProperty 'sun.zip.disableMemoryMapping', 'true'
}

task stage {
    dependsOn 'bootRepackage'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Edgware.SR3'
    }
}

task releaseZip(type: Zip, dependsOn: 'bootRepackage') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    archiveName "JAlgoArena-Queue-${version}.zip"
    destinationDir(file('build/libs'))
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
