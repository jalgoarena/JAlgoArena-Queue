buildscript {
    ext.fasterxml_version = '2.9.5'
    ext.spring_kafka_version = '2.1.4.RELEASE'
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id 'org.springframework.boot' version '2.0.1.RELEASE'
    id "jacoco"
}

group 'jalgoarena'
description = """JAlgoArena Queue Service"""

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/versioning.gradle"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.RELEASE'
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-webflux",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.kafka:spring-kafka",
            "com.fasterxml.jackson.core:jackson-databind:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-core:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-annotations:$fasterxml_version",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$fasterxml_version",
            "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$fasterxml_version",
            "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$fasterxml_version",
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
            "org.jetbrains.kotlin:kotlin-reflect",
            'net.logstash.logback:logstash-logback-encoder:5.1'

    testCompile 'junit:junit:4.12',
                "org.springframework.kafka:spring-kafka-test:$spring_kafka_version",
                "org.springframework.boot:spring-boot-starter-test",
                'com.nhaarman:mockito-kotlin-kt1.1:1.5.0',
                "org.assertj:assertj-core:3.9.1"
}

jar {
    baseName = 'jalgoarena-queue'
}

apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'clean', 'bootJar'

task stage {
    dependsOn 'bootJar'
}

task releaseZip(type: Zip, dependsOn: 'bootJar') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    archiveName "JAlgoArena-Queue-${version}.zip"
    destinationDir(file('build/libs'))
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task buildDockerImage(type: Exec, dependsOn: 'bootJar') {
    commandLine 'docker',  'build', '-t', "jalgoarena/queue:${version}", '.'
}

task pushDockerImages(type: Exec, dependsOn: 'buildDockerImage') {
    commandLine 'docker',  'push', "jalgoarena/queue:${version}"
}
